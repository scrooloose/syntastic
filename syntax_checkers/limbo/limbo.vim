"============================================================================
"File:        limbo.vim
"Description: Syntax checking plugin for syntastic.vim
"Maintainer:  Alex Efros <powerman-asdf@ya.ru>
"License:     This program is free software. It comes without any warranty,
"             to the extent permitted by applicable law. You can redistribute
"             it and/or modify it under the terms of the Do What The Fuck You
"             Want To Public License, Version 2, as published by Sam Hocevar.
"             See http://sam.zoy.org/wtfpl/COPYING for more details.
"
"============================================================================

if exists("g:loaded_syntastic_limbo_limbo_checker")
    finish
endif
let g:loaded_syntastic_limbo_limbo_checker = 1

function! SyntaxCheckers_limbo_limbo_IsAvailable() dict
    return executable('limbo')
endfunction

function! SyntaxCheckers_limbo_limbo_GetLocList() dict
    if filereadable('mkfile')
  	" don't generate .dis in current dir while checking syntax,
  	" .dis should be generated by `mk` and possibly in some other dir
    	let output = ' -o /dev/null'
    else
    	let output = ''
    endif

    let makeprg = self.makeprgBuild({
        \ 'exe': 'limbo',
        \ 'args': '-I$INFERNO_HOME -w' . output })

    let errorformat = '%f:%l:%m'

    if match(expand('%'), '\.m$') != -1
    	let errorformat = '%-G%f:%l: near ` EOF ` : no implementation module,' . errorformat
    endif

    return SyntasticMake({
    	\ 'makeprg': makeprg,
    	\ 'errorformat': errorformat })
endfunction

call g:SyntasticRegistry.CreateAndRegisterChecker({
    \ 'filetype': 'limbo',
    \ 'name': 'limbo' })
